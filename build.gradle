plugins {
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '1.3.1'
}

repositories {
    mavenCentral()
}

def javaVersion = JavaVersion.VERSION_1_8

sourceCompatibility = javaVersion
targetCompatibility = javaVersion

configurations {
    functionalTestImplementation.extendsFrom testImplementation
    functionalTestRuntimeOnly.extendsFrom testRuntimeOnly
}

dependencies {
    implementation 'de.undercouch:gradle-download-task:5.6.0'
    compileOnly 'org.projectlombok:lombok:1.18.42'
    annotationProcessor 'org.projectlombok:lombok:1.18.42'

    testImplementation 'org.assertj:assertj-core:3.27.6'
    testImplementation platform('org.junit:junit-bom:5.14.0')
    testImplementation 'org.junit.jupiter:junit-jupiter-api'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    functionalTestImplementation 'com.github.tomakehurst:wiremock-jre8:2.35.2'
    functionalTestImplementation 'org.apache.commons:commons-compress:1.28.0'
}

test {
    useJUnitPlatform()
}

version '1.6.1-SNAPSHOT'
group 'io.miret.etienne.gradle'

pluginBundle {
    website = 'https://github.com/EtienneMiret/sass-gradle-plugin'
    vcsUrl = 'https://github.com/EtienneMiret/sass-gradle-plugin'
    tags = ['sass', 'scss']
}

gradlePlugin {
    plugins {
        sass {
            id = 'io.miret.etienne.sass'
            displayName = 'Sass Compile'
            description = 'A Gradle plugin to compile scss files using the official Dart Sass compiler.'
            implementationClass = 'io.miret.etienne.gradle.sass.SassGradlePlugin'
        }
    }
}

sourceSets {
    functionalTest {
    }
}

gradlePlugin.testSourceSets(sourceSets.functionalTest)

task functionalTest(type: Test) {
    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath
    useJUnitPlatform()
}

check {
    dependsOn(tasks.functionalTest)
}

javadoc {
    failOnError false
}

publishPlugins {
    if (JavaVersion.current() != javaVersion) {
        throw new GradleException ("This build must be ran with Java ${javaVersion}.")
    }
}
