plugins {
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '0.15.0'
}

repositories {
    mavenCentral()
}

def javaVersion = JavaVersion.VERSION_1_8

sourceCompatibility = javaVersion
targetCompatibility = javaVersion

configurations {
    functionalTestImplementation.extendsFrom testImplementation
    functionalTestRuntimeOnly.extendsFrom testRuntimeOnly
}

dependencies {
    implementation 'de.undercouch:gradle-download-task:5.0.1'
    compileOnly 'org.projectlombok:lombok:1.18.22'
    annotationProcessor 'org.projectlombok:lombok:1.18.22'

    testImplementation 'org.assertj:assertj-core:3.22.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'

    functionalTestImplementation 'com.github.tomakehurst:wiremock-jre8:2.32.0'
    functionalTestImplementation 'org.apache.commons:commons-compress:1.19'
}

test {
    useJUnitPlatform()
}

version '1.2.0-SNAPSHOT'
group 'io.miret.etienne.gradle'

pluginBundle {
    website = 'https://github.com/EtienneMiret/sass-gradle-plugin'
    vcsUrl = 'https://github.com/EtienneMiret/sass-gradle-plugin'
    tags = ['sass', 'scss']
}

gradlePlugin {
    plugins {
        sass {
            id = 'io.miret.etienne.sass'
            displayName = 'Sass Compile'
            description = 'A Gradle plugin to compile scss files using the official Dart Sass compiler.'
            implementationClass = 'io.miret.etienne.gradle.sass.SassGradlePlugin'
        }
    }
}

sourceSets {
    functionalTest {
    }
}

gradlePlugin.testSourceSets(sourceSets.functionalTest)

task functionalTest(type: Test) {
    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath
    useJUnitPlatform()
}

check {
    dependsOn(tasks.functionalTest)
}

javadoc {
    failOnError false
}

publishPlugins {
    if (JavaVersion.current() != javaVersion) {
        throw new GradleException ("This build must be ran with Java ${javaVersion}.")
    }
}
